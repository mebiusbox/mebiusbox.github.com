<!DOCTYPE html>
<html lang="en">
  <head>
    <title>three.js</title>
    <meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
    <style>
      body {
        color: #fff;
        font-family: Monospace;
        font-size: 13px;
        text-align: center;
        font-weight: bold;
        
        background-color: #000;
        margin: 0px;
        overflow: hidden;
      }
      
      #info {
        position: absolute;
        padding: 10px;
        width: 100%;
        text-align: center;
        color: #fff;
      }
      
      #progress { 
        color: red,
        top: 7em;
        width: 100px;
        font-size: 3em;
        font-variant: samll-caps;
        font-weight: bold;
        position: absolute;
        z-index: 100;
        text-align: center;
        text-shadow: #000 0px 0px 10px;
        display: none;
      }
      
      #progressbar {
        text-align: center;
        background: white;
        width: 250px;
        height: 10px;
      }
      
      #bar {
        background: #d00;
        width: 50px;
        height: 10px;
      }
      
      a { color: orange; }
    </style>
  </head>
  <body>
    <div id="info">
      <a href="https://github.com/mebiusbox/pixy.js">pixy.js</a> + <a href="http://threejs.org" target="_blank">three.js</a> - ToneMapping
    </div>
    <div id="progress">
      <span id="message">Loading...</span>
      <center>
        <div id="progressbar" class="shadow"><div id="bar" class="shadow"></div></div>
      </center>
    </div>
    <script type="importmap">
			{
				"imports": {
					"three": "https://unpkg.com/three@0.158.0/build/three.module.js",
					"three/addons/": "https://unpkg.com/three@0.158.0/examples/jsm/",
					"pixy": "../build/pixy.module.min.js"
				}
			}
		</script>
		<script type="module" src="shader_tonemap.js"></script>
    <script id="fs-hdr" type="x-shader/x-fragment">
        uniform sampler2D tDiffuse;
        uniform float     exposure;
        uniform float     brightMax;
        uniform float     toneMapType;
        // GT Tone mapping
        uniform float GT_P; // Maximum Brightness [1..100]
        uniform float GT_a; // Contrast [1..5]
        uniform float GT_m; // Linear section start [0..1]
        uniform float GT_l; // Linear section length [0..1]
        uniform float GT_c; // Black tightness-c [1..3]
        uniform float GT_b; // Black tightness-b [0..1]
        // Synthesis
        uniform float Lwa;
        uniform float Ldmax;
        uniform float Lscale;
        uniform float Lwhite;
        varying vec2 vUv;
        vec3 decode(const in vec4 color) {
            vec4 rgbcolor = vec4(0.0, 0.0, 0.0, 0.0);
            if (color.w > 0.0) {
                //float f = pow(2.0, 127.0*(color.w-0.5));
                //rgbcolor.xyz = color.xyz * f;
                rgbcolor.xyz = color.xyz * exp2(color.w*255.0-128.0);
            }
            return rgbcolor.xyz;
        }
        vec4 ToneMappingGT(const in vec4 color) {
            vec4 x = vec4(color.xyz, 1.0);
            vec4 P = vec4(GT_P);
            vec4 a = vec4(GT_a);
            vec4 m = vec4(GT_m);
            vec4 l = vec4(GT_l);
            vec4 c = vec4(GT_c);
            vec4 b = vec4(GT_b);
            // Linear Region Computation
            // l0 is the linear length after scale
            vec4 l0 = ((P-m)/l)/a;
            vec4 L0 = m - (m/a);
            vec4 L1 = m + (l-m) / a;
            vec4 Lx = m + a*(x - m);

            // Toe
            vec4 Tx = m * pow(x/m, c) + b;

            // Shoulder
            vec4 S0 = m + l0;
            vec4 S1 = m + a*l0;
            vec4 C2 = (a*P)/(P-S1);
            vec4 Sx = P - (P-S1) * exp(-(C2*(x-S0)/P));

            // Toe weight
            vec4 w0 = vec4(1.0) - smoothstep(vec4(0), m, x);
            // Shoulder weight
            vec4 w2 = smoothstep(m+l0, m+l0, x);
            // Linear start
            vec4 w1 = vec4(1.0) - w0 - w2;

            return Tx*w0+Lx*w1+Sx*w2;
        }
        vec4 ToneMappingACES(vec4 color) {
            vec4 a = vec4(2.51);
            vec4 b = vec4(0.03);
            vec4 c = vec4(2.43);
            vec4 d = vec4(0.59);
            vec4 e = vec4(0.14);
            return (color*(a*color+b))/(color*(c*color+d)+e);
        }
        float tp(float La) {
          float logLa = log(La)/log(10.0);
          float result;
          if (logLa <= -2.6) {
            result = -0.72;
          }
          else if (logLa >= 1.9) {
            result = logLa - 1.255;
          }
          else {
            result = pow(0.249 * logLa + 0.65, 2.7) - 0.72;
          }
          return pow(10.0, result);
        }
        float ts(float La) {
          float logLa = log(La)/log(10.0);
          float result;
          if (logLa <= -3.94) {
            result = -2.86;
          }
          else if (logLa >= -1.44) {
            result = logLa - 0.395;
          }
          else {
            result = pow(0.405*logLa + 1.6, 2.18) - 2.86;
          }
          return pow(10.0, result);
        }
        float getLuminance(vec4 color) {
          return 0.212671*color.r + 0.71516*color.g + 0.072169*color.b;
        }
        vec4 ToneMappingSynthesis(vec4 color) {
          float Lda = Ldmax / 2.0;
          float L = getLuminance(exposure * color);
          float mP = tp(Lda) / tp(exposure * Lwa);
          float mS = ts(Lda) / ts(exposure * Lwa);
          float k = (1.0 - (Lwa/2.0 - 0.01) / (10.0-0.01));
          k = clamp(k*k, 0.0, 1.0);
          float Ld = mP * L + k*mS * L;
          color = Ld * color / L;
          color = clamp(color, 0.0, 1.0);
          return color;
        }
        vec4 ToneMappingSynthesis2(vec4 color) {
          vec4 Lm = Lscale * color;
          vec4 L1 = Lm * (1.0 + Lm/(Lwhite*Lwhite));
          return L1 / (1.0 + Lm);
        }
				vec4 GammaToLinear(in vec4 value, in float gammaFactor) {
					return vec4(pow(value.xyz, vec3(gammaFactor)), value.w);
				}
				vec4 LinearToGamma(in vec4 value, in float gammaFactor) {
					return vec4(pow(value.xyz, vec3(1.0/gammaFactor)), value.w);
				}
				vec4 RGBEToLinear( in vec4 value ) {
					return vec4( value.rgb * exp2( value.a * 255.0 - 128.0 ), 1.0 );
				}
				vec4 LinearToRGBE( in vec4 value ) {
					float maxComponent = max( max( value.r, value.g ), value.b );
					float fExp = clamp( ceil( log2( maxComponent ) ), -128.0, 127.0 );
					return vec4( value.rgb / exp2( fExp ), ( fExp + 128.0 ) / 255.0 );
				}
        void main() {
            // Decode HDR
            // vec4 color = RGBEToLinear(texture2D(tDiffuse, vUv));
						vec4 color = texture2D(tDiffuse, vUv);

            // Tone mapping
            if (toneMapType > 2.5) {
                color = ToneMappingSynthesis2(color);
            }
            else if (toneMapType > 1.5) {
                color = ToneMappingACES(color);
            }
            else if (toneMapType > 0.5) {
                color = ToneMappingGT(color);
            }
            else {
                float YD = exposure * (exposure/brightMax + 1.0) / (exposure + 1.0);
                color *= YD;
            }

            // To sRGB
            vec4 final = clamp(LinearToGamma(color, 2.2), 0.0, 1.0);
            gl_FragColor = vec4(final.xyz, 1.0);
        }
    </script>
    <script id="vs-hdr" type="x-shader/x-vertex">
        varying vec2 vUv;
        void main() {
            vUv = vec2(uv.x, 1.0-uv.y);
            gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
        }
    </script>
  </body>
</html>
